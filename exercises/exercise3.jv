pipeline CountryStatsPipeline {

    CountryStatsExtractor
        -> CountryStatsXLSXInterpreter
        -> CountryStatsSheetPicker
        -> CountryStatsCellRangeSelector
        -> CountryStatsCellWriter;

    CountryStatsCellWriter
        -> CountryStatsTableInterpreterBondIssuance
        -> CountryStatsLoaderBondIssuance;

    CountryStatsCellWriter
        -> CountryStatsTableInterpreterGDP
        -> CountryStatsLoaderGDP;

    block CountryStatsExtractor oftype HttpExtractor {
        url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
    }

    block CountryStatsXLSXInterpreter oftype XLSXInterpreter { }

    block CountryStatsSheetPicker oftype SheetPicker {
        sheetName: "Figure S5.1.2";
    }

    block CountryStatsCellRangeSelector oftype CellRangeSelector {
        select: range P2:S45;
    }

    block CountryStatsCellWriter oftype CellWriter {
        at: range A1:D1;
        write: [
            "Country Code",
            "Economy",
            "GDP per Capita",
            "Bond Issuance Share"
        ];
    }

    block CountryStatsTableInterpreterBondIssuance oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "Bond Issuance Share" oftype bondIssuanceShare
        ];
    }

    block CountryStatsTableInterpreterGDP oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "GDP per Capita" oftype gdp,
        ];
    }

    block CountryStatsLoaderBondIssuance oftype SQLiteLoader {
        table: "bondIssuance";
        file: "./country-stats.sqlite";
    }

    block CountryStatsLoaderGDP oftype SQLiteLoader {
        table: "gdpPerCapita";
        file: "./country-stats.sqlite";
    }
}

valuetype gdp oftype decimal {
    constraints: [
        positive
    ];
}

valuetype bondIssuanceShare oftype decimal {
    constraints: [
        zeroone
    ];
}

constraint positive oftype RangeConstraint {
    lowerBound: 0;
    lowerBoundInclusive: false;
}

constraint zeroone oftype RangeConstraint {
    lowerBound: 0;
    upperBound: 1;
    lowerBoundInclusive: true;
    upperBoundInclusive: true;
}
